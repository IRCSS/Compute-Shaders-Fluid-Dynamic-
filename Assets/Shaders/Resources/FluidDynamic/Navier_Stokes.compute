// This file contains the Navier Stokes equation. The Navier Stokes equation are of the form:
// new_Divergent_vector_field = advection + diffusion + external_forces
// The diffusion is only a linear equation system which is solved by calling the solver several times
// hence this file only contains implemmentation for adding external forces and advection
// plus projection


//____________________________________________________________________________________
// KERNELS

#pragma kernel advection
#pragma kernel divergence
#pragma kernel calculate_divergence_free

//____________________________________________________________________________________
// COMMON DECLERNATIONS

#include "FluidDynamicCommonUniforms.cginc"


//____________________________________________________________________________________
// HELPER FUNCTIONS

#include "FluidDynamicUtility.cginc"

//____________________________________________________________________________________
// MAIN FUNCTIONS

// ADVECTION DECLERNATIONS

float                      _dissipationFactor;                              // This factor determines how fast the quantities dissapate over time. 

StructuredBuffer<float4>   _velocity_field_buffer;                          // The velocity field which does the carrying
StructuredBuffer<float4>   _field_to_advect_buffer;                         // this is the field which is being carried off by the vector field, it can be temperature, dye, or the velocity itself

RWStructuredBuffer<float4> _new_advected_field;                             // The results of advection is written on this field

//----------------------------------------------------------------------------
// Function         : Advection
// Description      : Advection is the part of the equation that is responsible for carrying
//                    things along the fluid field. This can be head, densities or even the 
//                    the velocity of the fluid itself. It works by tracing backwards in time
//                    and seeing what qunatities the current velocity would have brought to the 
//                    cell which is being examined.
//                    It performs bilinear interpolation at the destination to get a smooth
//                    resulting field. It also clamps the velocities on the bounderies.

//----------------------------------------------------------------------------

[numthreads(16,16,1)]                                                        // Runs once per simulation grid
void advection(uint3 id : SV_DispatchThreadID)                              
{                                                                           
    float2  coord = id.xy;                                                   // One to one mapping between the dispatch ids and the coordinate
    
    float2 advection_source_coord = coord -                                  // Trace backwards along trajectory
                                  (i_timeStep *                              // distance = velocity * time, this is the time part 
                                  _velocity_field_buffer[id2Dto1D(coord)] *  // This is the velocity part
                                  (1. / i_grid_scale)) ;                     // The speed is in the unite of grids, but since the grid might not be unit-scale, we need to also scale by the grid cell size.
    
    
    advection_source_coord = clamp(advection_source_coord,                   // Handling the case where the velocity traces back to outside the bounds of the buffer
                                   0.0, i_Resolution - 1.0);                 // left hand side ensures the smallest element to read is element , and the biggest n-1
    
    _new_advected_field[id2Dto1D(coord)] = _dissipationFactor *              // Disspation factor 1 = lasts forever, 0 = instantly dissipates, dunno how physcilly accurate this is. 
                                           StructuredBufferBilinearLoad(_field_to_advect_buffer, advection_source_coord);
   // _new_advected_field[id2Dto1D(coord)] = _dissipationFactor * _field_to_advect_buffer[id2Dto1D(advection_source_coord)];
}


// _______________________________

// DIVERGENCE DECLERNATIONS

StructuredBuffer<float4>     _divergence_vector_field;                      // This is the vector field which we want to calculate the divergence of, the members of the array are quanteties in this vector field
RWStructuredBuffer<float4>   _divergence_values;                            // The results are written to this buffer


//----------------------------------------------------------------------------
// Function         : divergence
// Description      : Divergence is a physical measure of how much stuff flow in 
//                    or out of a point in a vector field. Since fluids can not be 
//                    compressed, the fluid field needs to have a divergence of 0 everywhere.
//                    The quanteties we calculate are at first not divergence free, however by
//                    calculating its divergence and using a possion equation we can split up the 
//                    the vector field in a divergence and curl components, and keep the curl one.
//                    For a more none math explaination, look at my blog post Gentle Introduction to Fluid Simulation for Programmers and Techincal Artists
//                    Divergence = partial(field.x)/ partial(x) + partial(field.y)/partial(y)
//                    The partial(x) = partial(y) = 1.0/(2.0*cellSize)
//----------------------------------------------------------------------------

[numthreads(16, 16, 1)]                                                     // runs once per cell of the vector field
void divergence(uint3 id : SV_DispatchThreadID)
{                                                                           
    float partialXY = 2. * i_grid_scale;
    int2  coord     = id.xy;                                                // one to one mapping between thread ids in the dispatch and the structured buffer

    float4 left     = _divergence_vector_field[id2Dto1D(coord - int2(1, 0))];
    float4 right    = _divergence_vector_field[id2Dto1D(coord + int2(1, 0))];
    float4 bottom   = _divergence_vector_field[id2Dto1D(coord - int2(0, 1))];
    float4 top      = _divergence_vector_field[id2Dto1D(coord + int2(0, 1))];

    _divergence_values[id2Dto1D(coord)] = 
                       ((right.x - left.x + top.y - bottom.y) *  1. / partialXY).xxxx;
}
    
// _______________________________

// CALCULATE_DIVERGENCE_FREE DECLERNATIONS

StructuredBuffer<float4>    _non_zero_divergence_velocity_field;            // This is the velocity field which we have calculated after advection, diffusion and adding external forces
StructuredBuffer<float4>    _pressure_field;                                // This is actually a scalar float field.But to be able to use a generic solver for the possion equation as well as diffusion I am using a float4 structured buffer. The pressure field is calculated with the solve from the euqation lepralce(pressure_field) = divergence(velocity_field) 
RWStructuredBuffer<float4>  _pressure_gradient;                             // It is not nesscerly to save this on device memory, it could also be saved in the thread register and immedietly subtracted from the non zero divergent vector field, however for debug porpuses, I write this to the vram
RWStructuredBuffer<float4>  _divergence_free_field;                         // the result of the projection is written here

//----------------------------------------------------------------------------
// Function         : calculate_divergence_free
// Description      : Last step of the navier stoke equations. After the pressure disturbance 
//                    caused by the non zero diveragnce vector field is calculated through the possion equation, 
//                    the acceleration caused by this uneven pressure field is calculated by taking the gradient of this
//                    pressure field and subtracted from the non zero divergence vector field. 
//                    
//                    divergence_free_vector_field = non_zero_divergence_vector_field - gradient(pressure_field)
//----------------------------------------------------------------------------
[numthreads(16, 16, 1)]                                                     // runs once per cell of the vector field
void calculate_divergence_free(uint3 id : SV_DispatchThreadID)
{                                                                           
     float partialXY = 2. * i_grid_scale;
     int2 coord      = id.xy;                                               // one to one mapping between thread ids in the dispatch and the structured buffer

     float4 gradient_value = gradient(_pressure_field, partialXY, coord);

     _pressure_gradient[id2Dto1D(coord)] = gradient_value;                  // For debug and visualisation porpuses

     _divergence_free_field[id2Dto1D(coord)] = 
            _non_zero_divergence_velocity_field[id2Dto1D(coord)] - gradient_value;

}